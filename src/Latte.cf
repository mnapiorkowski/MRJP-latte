-- programs ------------------------------------------------

entrypoints     Program ;
Progr.          Program ::= [TopDef] ;
FnDef.	        TopDef ::= TType Ident "(" [Arg] ")" Block ;
separator       nonempty TopDef "" ;

AArg.           Arg ::= TType Ident ;
separator       Arg "," ;

-- statements ----------------------------------------------

BBlock.         Block ::= "{" [Stmt] "}" ;
separator       Stmt "" ;
SEmpty.         Stmt ::= ";" ;
SBlock.         Stmt ::= Block ;
SExp.           Stmt ::= Expr  ";" ;

SDecl.          Stmt ::= TType [Item] ";" ;
NoInit.         Item ::= Ident ;
Init.           Item ::= Ident "=" Expr ;
separator       nonempty Item "," ;

LVar.           LVal ::= Ident ;
LArr.           LVal ::= Ident "[" Expr "]" ;
SAss.           Stmt ::= LVal "=" Expr  ";" ;
SIncr.          Stmt ::= LVal "++"  ";" ;
SDecr.          Stmt ::= LVal "--"  ";" ;

SRet.           Stmt ::= "return" Expr ";" ;
SVRet.          Stmt ::= "return" ";" ;

SIf.            Stmt ::= "if" "(" Expr ")" Stmt ;
SIfElse.        Stmt ::= "if" "(" Expr ")" Stmt "else" Stmt ;

SWhile.         Stmt ::= "while" "(" Expr ")" Stmt ;
SFor.           Stmt ::= "for" "(" TType Ident ":" Expr ")" Stmt ;

-- Types ---------------------------------------------------

TInt.           TType ::= "int" ;
TBool.          TType ::= "boolean" ;
TString.        TType ::= "string" ;
TVoid.          TType ::= "void" ;
TArray.         TType ::= TType "[]" ;

-- Expressions ---------------------------------------------

ELitInt.        Expr7 ::= Integer ;
ELitTrue.       Expr7 ::= "true" ;
ELitFalse.      Expr7 ::= "false" ;
EString.        Expr7 ::= String ;

ELVal.          Expr6 ::= LVal ;
EApp.           Expr6 ::= Ident "(" [Expr] ")" ;
ENewArr.        Expr6 ::= "new" TType "[" Expr "]" ;

EAttr.          Expr5 ::= Expr6 "." Ident ;
ENeg.           Expr5 ::= "-" Expr6 ;
ENot.           Expr5 ::= "!" Expr6 ;

EMul.           Expr4 ::= Expr4 MulOp Expr5 ;

EAdd.           Expr3 ::= Expr3 AddOp Expr4 ;

ERel.           Expr2 ::= Expr2 RelOp Expr3 ;

EAnd.           Expr1 ::= Expr2 "&&" Expr1 ;

EOr.            Expr ::= Expr1 "||" Expr ;

coercions       Expr 7 ;
separator       Expr "," ;

-- operators -----------------------------------------------

OPlus.          AddOp ::= "+" ;
OMinus.         AddOp ::= "-" ;

OTimes.         MulOp ::= "*" ;
ODiv.           MulOp ::= "/" ;
OMod.           MulOp ::= "%" ;

OLt.            RelOp ::= "<" ;
OLeq.           RelOp ::= "<=" ;
OGt.            RelOp ::= ">" ;
OGeq.           RelOp ::= ">=" ;
OEq.            RelOp ::= "==" ;
ONeq.           RelOp ::= "!=" ;

-- comments ------------------------------------------------

comment         "#" ;
comment         "//" ;
comment         "/*" "*/" ;
